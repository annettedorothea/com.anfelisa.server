com.anfelisa.user

target JAVA

ACE {
	GET GetUserProfile uses User
	GET UsernameAvailable uses UsernameAvailable
	GET GetRole uses Role
	GET GetAllUsers uses UserList

	PUT UpdateUser uses UserUpdate {
		on success ( User.updateUser )
	}
	POST ForgotPassword uses ForgotPassword {
		on ok ( ResetPassword.insert Email.sendForgotPasswordEmail)
		on doesNotExist
	}
	PUT ResetPassword uses ResetPassword {
		on ok (User.resetPassword ResetPassword.delete)
	}
	POST RegisterUser uses UserRegistration {
		on ok (User.registerUser EmailConfirmation.insert Email.sendRegistrationEmail)
	}
	PUT ConfirmEmail uses EmailConfirmation {
		on ok (User.confirmEmail EmailConfirmation.delete)
	}
	PUT ChangeUserRole uses ChangeUserRole {
		on ok (User.changeUserRole)
	} 
	DELETE DeleteUser uses DeleteUser {
		on ok (User.deleteUser)
	}
}

views {
	User {
		updateUser(UserUpdate)
		registerUser(UserRegistration)
		confirmEmail(EmailConfirmation)
		changeUserRole(ChangeUserRole)
		deleteUser(Username)
		resetPassword(ResetPassword)
	}
	EmailConfirmation {
		insert(UserRegistration)
		delete(EmailConfirmation)
	}
	ResetPassword {
		insert(ForgotPassword)
		delete(ResetPassword)
	}
	external Email {
		sendForgotPasswordEmail(ForgotPassword)
		sendRegistrationEmail(UserRegistration)
	}
}

models {
	schema public
	
	persistent User {
		Unique PrimaryKey NotNull String userId
		Unique NotNull String username
		NotNull String password
		NotNull String email
		NotNull String role
		NotNull Boolean emailConfirmed
	}

	persistent EmailConfirmation {
		Unique PrimaryKey NotNull String token
		NotNull String userId
	}

	persistent ResetPassword {
		Unique PrimaryKey NotNull String token
		NotNull String userId
	}

	Credentials {
		String credentialsUsername
		String credentialsRole
	} 
	Role {
		String role
	}
	Username {
		NotNull String username
	} 
	UserId {
		NotNull String userId
	} 
	Email {
		NotNull String email
	} 
	Available {
		Boolean available
	}
	UserRoleUpdate {
		NotNull String editedUsername
		NotNull String role
	}
	DeleteUser {
		NotNull String deletedUsername
	}
	Language {
		NotNull String language
	}
	UserList {
		List User userList
	}
	Password {
		String password
	}
}

data {
	User : ( User )
	UserUpdate : (UserRoleUpdate Credentials)
	PasswordUpdate : (Credentials)
	ForgotPassword : (ResetPassword Language Email Username)
	UsernameAvailable : (Username Available)
	UserRegistration : (User Language EmailConfirmation)
	DeleteUser : ( Credentials DeleteUser)
	Username : (Username)
	ChangeUserRole : (UserId Role)
	Role : (Credentials)
	UserList : (UserList)
	EmailConfirmation: (EmailConfirmation)
	ResetPassword: (ResetPassword Password)
}

