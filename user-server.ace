com.anfelisa.user

target JAVA

models {
	persistent User {
		primitiveAttributes {
			Unique PrimaryKey NotNull String username
			NotNull String password
			NotNull String name
			NotNull String prename
			NotNull String email
			NotNull String role
		}
	}

	persistent LoginLog {
		primitiveAttributes {
			Unique PrimaryKey NotNull Serial loginLogId
			NotNull String username
			NotNull DateTime date
		}
	} 

	Credentials {
		primitiveAttributes {
			NotNull String username
			NotNull String role
		}
	} 

	Points {
		primitiveAttributes {
			NotNull Integer points
		}
	} 

	Username {
		primitiveAttributes {
			NotNull String username
		}
	} 
	UsernameAndPassword {
		primitiveAttributes {
			NotNull String username
			NotNull String password
		}
	} 
	UserUpdate {
		primitiveAttributes {
			NotNull String username
			NotNull String name
			NotNull String prename
			NotNull String email
		}
	}
	CourseSelection {
		primitiveAttributes {
			List Integer courseIdList
		}
	}
	
	CourseId {
		primitiveAttributes {
			Integer courseId
		}
	}
	
	ForgotPassword {
		primitiveAttributes {
			NotNull String username
			NotNull String email
			NotNull String name
			NotNull String prename
			NotNull String language
			NotNull String password
		}
	}
}

data {
	UserCreation : ( User )
	Login : ( LoginLog Credentials )
	Points : ( Points Credentials )
	UserInfo : (User com.anfelisa.course.CourseList com.anfelisa.box.MyBoxList Username)
	UserUpdate : (UserUpdate)
	CourseSelection : (com.anfelisa.course.CourseList Username)
	AddCourses : (CourseSelection Username)
	RemoveCourse : (CourseId Username)
	PasswordUpdate : (UsernameAndPassword)
	ForgotPassword : (ForgotPassword)
}

actions {
	POST CreateUser uses UserCreation executes CreateUser 
	POST Login uses Login executes Login
	GET GetPoints uses Points
	GET GetUserInfo uses UserInfo
	PUT UpdateUser uses UserUpdate executes UpdateUser
	GET GetCourseSelection uses CourseSelection
	POST AddCourses uses AddCourses executes AddCourses
	DELETE RemoveCourse uses RemoveCourse executes RemoveCourse
	PUT UpdatePassword uses PasswordUpdate executes UpdatePassword
	POST ForgotPassword uses ForgotPassword executes ForgotPassword
}

commands {
	CreateUser uses UserCreation {
		on go publishes (UserCreated)
	}
	Login uses Login {
		on success publishes (UserLoggedIn)
	}
	UpdateUser uses UserUpdate {
		on success publishes ( UserUpdated )
	}
	AddCourses uses AddCourses {
		on success publishes (CoursesAdded)
	}
	RemoveCourse uses RemoveCourse {
		on success publishes (CourseRemoved)
	}
	UpdatePassword uses PasswordUpdate {
		on success publishes (PasswordUpdated)
	}
	ForgotPassword uses ForgotPassword {
		on ok publishes (SendEmail)
		on userNotFound 
	}
}

events {
	UserCreated uses UserCreation listenedToBy ( User.createUser	)
	UserLoggedIn uses Login listenedToBy ( LoginLog.userLoggedIn )
	UserUpdated uses UserUpdate listenedToBy (User.updateUser)
	CoursesAdded uses AddCourses listenedToBy (User.addCourses)
	CourseRemoved uses RemoveCourse listenedToBy ( User.removeCourse )
	PasswordUpdated uses PasswordUpdate listenedToBy (User.updatePassword)
	SendEmail uses ForgotPassword listenedToBy (User.sendForgotPasswordEmail)
}

views {
	User {
		createUser(UserCreation)
		updateUser(UserUpdate)
		addCourses(AddCourses)
		removeCourse(RemoveCourse)
		updatePassword(PasswordUpdate)
		sendForgotPasswordEmail(ForgotPassword)
	}
	LoginLog {
		userLoggedIn(Login)
	}
}