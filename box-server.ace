com.anfelisa.box

target JAVA

ACE {
	GET GetBoxes uses BoxList
	GET GetBox uses BoxInfo
	POST CreateBox uses BoxCreation {
		on ok ( Box.createBox )
	}
	PUT UpdateBox uses BoxUpdate {
		on ok ( Box.updateBox )
	}
	PUT PostponeCards uses PostponeCards {
		on ok ( ScheduledCard.postponeCards )
		on noDelay
	}
	DELETE DeleteBox uses DeleteBox {
		on ok (Box.deleteBox)
	}
	
	GET LoadNextCard uses NextCard

	GET LoadNextReinforceCard uses NextReinforceCard

	POST ScheduleNextCard uses ScheduleCard {
		on ok (Box.scheduleCard)
	}
	
	POST ScheduleCards uses ScheduleCards {
		on ok (Box.scheduleCards)
		on nullOrEmpty
	}
	
	POST ScoreCard uses ScoreCard {
		on score (ScheduledCard.score ScheduledCard.scheduleNext)
		on scoreAndReinforce (ScheduledCard.score ScheduledCard.scheduleNext ReinforceCard.add)
	}
	POST ScoreReinforceCard uses ScoreReinforceCard {
		on remove (ReinforceCard.remove)
		on keep (ReinforceCard.update)
	}
	
}

views {
	Box {
		createBox (BoxCreation)
		updateBox (BoxCreation)
		deleteBox (DeleteBox)
		scheduleCard(ScheduleCard)
		scheduleCards(ScheduleCards)
	}
	ScheduledCard {
		score(ScoreCard)
		scheduleNext(ScoreCard)
		postponeCards(PostponeCards)
	}
	ReinforceCard {
		add(ScoreCard)
		remove(ScoreReinforceCard)
		update(ScoreReinforceCard)
	}
}

models {
	schema public
	persistent Box {
		Unique PrimaryKey NotNull String boxId
		NotNull String userId references com.anfelisa.user.User.userId
		NotNull String categoryId references com.anfelisa.category.Category.categoryId
		Integer maxInterval
	}

	persistent ScheduledCard {
		Unique PrimaryKey NotNull String scheduledCardId
		NotNull String cardId references com.anfelisa.card.Card.cardId
		NotNull String boxId references Box.boxId
		NotNull DateTime createdDate
		NotNull Float ef
		Integer interval
		NotNull Integer n
		NotNull Integer count
		NotNull DateTime scheduledDate
		Integer lastQuality
		Integer quality
		DateTime scoredDate
	}

	persistent ReinforceCard {
		Unique PrimaryKey NotNull String reinforceCardId
		Unique PrimaryKey NotNull String scheduledCardId references ScheduledCard.scheduledCardId
		NotNull String boxId references Box.boxId
		NotNull DateTime changeDate
	}

	CategorySelectionList {
		List CategorySelectionItem categoryList
	}
	
	CategorySelectionItem {
		NotNull String categoryId
		NotNull String categoryName
		NotNull Integer categoryIndex
	}
	BoxInfo {
		Integer todaysCards
		Integer totalCards
		Integer reinforceCards
		Integer myCards
		Integer daysBehindSchedule
		NotNull String boxId
		NotNull String categoryId
		NotNull String categoryName
		NotNull Integer categoryIndex
		Integer maxInterval
		Integer quality0Count
		Integer quality1Count
		Integer quality2Count
		Integer quality3Count
		Integer quality4Count
		Integer quality5Count
	}
	BoxList {
		List BoxInfo boxList
		NotNull String userId
		NotNull DateTime today
	}
	Today {
		NotNull DateTime today
	}
	PostponeCards {
		NotNull String boxId
		NotNull String userId
		NotNull Integer days
	}

	BoxUpdate {
		NotNull String userId
		NotNull String boxId
		Integer maxInterval
	}

	DeleteBox {
		NotNull String userId
		NotNull String boxId
	}

	NextCard {
		NotNull String scheduledCardId
		NotNull String cardId
		NotNull String boxId
		NotNull DateTime scheduledDate
		Integer lastQuality
		String given
		String wanted
		String image
		String categoryId
		String rootCategoryId
		NotNull Integer count
		DateTime scoredDate
	}
	
	BoxId {
		NotNull String boxId
	}
	
	ScheduleCategory {
		NotNull String userId
		NotNull String boxId
		NotNull String categoryId
		List ScheduledCard scheduledCards
	}
	
	ScoreCard {
		NotNull String cardId
		NotNull String boxId
		
		NotNull String nextScheduledCardScheduledCardId
		NotNull DateTime nextScheduledCardCreatedDate
		NotNull Float nextScheduledCardEf
		Integer nextScheduledCardInterval
		NotNull Integer nextScheduledCardN
		NotNull Integer nextScheduledCardCount
		NotNull DateTime nextScheduledCardScheduledDate
		Integer nextScheduledCardLastQuality
		
		NotNull String scoredCardScheduledCardId
		Integer scoredCardQuality
		DateTime scoredCardScoredDate

		NotNull String reinforceCardId
		NotNull DateTime reinforceCardCreatedDate
	}
	
	NextReinforceCard {
		NotNull String reinforceCardId
		NotNull DateTime changeDate
		Integer lastQuality
		String given
		String wanted
		String image
		String categoryId
	}
	ScoreReinforceCard {
		NotNull String reinforceCardId
		NotNull Integer quality
		NotNull DateTime changeDate
	}
	
	ScheduledCards {
		List ScheduledCard newScheduledCards
		List String existingScheduledCardIds
		List String cardIds
		DateTime scheduledDate
	}
}

data {
	CategoryList: (CategorySelectionList com.anfelisa.user.UserId)
	BoxList : ( BoxList )
	BoxInfo : ( BoxInfo Today com.anfelisa.user.UserId )
	PostponeCards : (PostponeCards Today)
	DeleteBox : (DeleteBox)
	BoxUpdate : ( BoxUpdate )
	BoxCreation : ( Box )
	NextCard : ( NextCard Today com.anfelisa.user.UserId)
	ScoreCard : (com.anfelisa.user.UserId ScoreCard)
	ScoreReinforceCard : (com.anfelisa.user.UserId ScoreReinforceCard)
	NextReinforceCard : (com.anfelisa.user.UserId NextReinforceCard BoxId)
	ScheduleCard : (ScheduledCard com.anfelisa.user.UserId BoxId )
	ScheduleCards : (ScheduledCards com.anfelisa.user.UserId BoxId )
}
