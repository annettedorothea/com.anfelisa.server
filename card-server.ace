HttpServer Java Dropwizard JDBI3 Liquibase

com.anfelisa.card

import com.anfelisa.user.AuthUser

ACE 
	CreateCard<CardCreation>
		POST "/card/create" 
		authorize 
		payload 
			NotNull wanted
			NotNull given
			NotNull categoryId
		on ok (Card.insert)
	
	UpdateCard<CardUpdate>
		PUT "/card/update" 
		authorize 
		payload 
			NotNull cardId
			NotNull given
			NotNull wanted
		on ok (Card.update)
	
	UpdateCardPriority<CardUpdatePriority>
		PUT "/card/update-priority" 
		authorize 
		payload 
			NotNull cardId
			priority
		on ok (Card.updatePriority)
	
	DeleteCard<CardDelete>
		DELETE "/card/delete"
		authorize 
		queryParams 
			NotNull cardIds
		on ok (Card.delete)
	
	GetCards<CardList>
		GET "/cards" 
		authorize 
		queryParams 
			NotNull categoryId 
			filterNonScheduled
			priority
			reverse
		response
			cardList
			
	GetDuplicates<CardSearch>
		GET "/cards/search"  
		authorize
		queryParams 
			given
			wanted
			naturalInputOrder 
			NotNull categoryId
		response
			cardList

	MoveCards<MoveCards>
		PUT "/cards/move"
		authorize
		payload
			NotNull cardIdList
			NotNull categoryId
		on ok (Card.moveCards)

	ChangeOrder<ChangeCardOrderList>
		PUT "/cards/changeorder"
		authorize
		payload
			NotNull cardIdList
			NotNull cardId
		on ok (Card.changeCardOrder)
		
	GetTranslation<CardTranslation>
		GET "/card/translation" 
		authorize 
		queryParams 
			NotNull sourceValue 
			NotNull sourceLanguage 
			NotNull targetLanguage
		response
			targetValue

views 
	Card {
		insert(CardCreation)
		delete(CardDelete)
		update(CardUpdate)
		updatePriority(CardUpdatePriority)
		moveCards(MoveCards)
		bulkInsert(CsvUpload)
		changeCardOrder(ChangeCardOrderList)
	}

models 
	persistent Card {
		Unique PrimaryKey NotNull String cardId
		String given
		String wanted
		NotNull String cardAuthor
		NotNull Integer cardIndex
		NotNull String categoryId references com.anfelisa.category.Category.categoryId
		NotNull String rootCategoryId references com.anfelisa.category.Category.categoryId
		Integer priority
	}

	CardUpdate {
		String cardId
		String given
		String wanted
		String userId
	}
	
	CardUpdatePriority {
		String cardId
		Integer priority
		String userId
	}
	
	CardDelete {
		String cardIds
		List CardDeleteItem cardDeleteItems 
		String userId
	}
	CardDeleteItem {
		String cardId
		Integer cardIndex
		String categoryId
	}
	CardSearch {
		String userId
		String given
		String wanted
		String categoryId
		List CardWithCategoryName cardList
		Boolean naturalInputOrder
	}
	
	CardWithCategoryName extends Card {
		String categoryName
	}
	
	CardTranslation {
		String sourceValue
		String targetValue
		String sourceLanguage
		String targetLanguage
	}
	
	CardCreation extends Card {
		String userId
		String username
	}
	
	CardList {
		String userId
		List CardWithInfo cardList
		String categoryId
		Boolean filterNonScheduled
		Integer priority
		Boolean reverse
	}
	
	CardWithInfo extends Card {
		DateTime next
	}
	
	MoveCards {
		List String cardIdList
		String categoryId	
		String userId
		List Card movedCards
		List Card updatedIndices
	}

	ChangeCardOrderList {
		List String cardIdList
		String cardId	
		String userId
		List Card updatedIndices
	}

	CsvUpload {
		String userId
		String categoryId
		List SimpleCard previewCsv
		List Card cards
	}
	
	SimpleCard {
		String given
		String wanted
		String id
	}



