HttpServer

com.anfelisa.card

import com.anfelisa.user.AuthUser

ACE 
	CreateCard<CardCreation>
		POST "/card/create" 
		authorize 
		payload 
			wanted
			given
			image optional
			categoryId
		on ok (Card.insert)
	
	UpdateCard<CardUpdate>
		PUT "/card/update" 
		authorize 
		payload 
			cardId
			given
			image optional
			wanted
		on ok (Card.update)
	
	DeleteCard<CardDelete>
		DELETE "/card/delete"
		authorize 
		queryParams cardId
		on ok (Card.delete)
	
	GetCards<CardList>
		GET "/cards" 
		authorize 
		queryParams 
			categoryId 
		response
			cardList
			
	GetDuplicates<CardSearch>
		GET "/cards/search"  
		authorize
		queryParams 
			given optional
			wanted optional
			naturalInputOrder optional 
			categoryId
		response
			cardList

	MoveCards<MoveCards>
		PUT "/cards/move"
		authorize
		payload
			cardIdList
			categoryId
		on ok (Card.moveCards)

	ChangeOrder<ChangeCardOrderList>
		PUT "/cards/changeorder"
		authorize
		payload
			cardIdList
			cardId
		on ok (Card.changeCardOrder)

	ImportCsv<CsvUpload>
		PUT "/category/import-csv"
		authorize
		payload
			previewCsv
			categoryId
		on ok (Card.bulkInsert)
		
	proxy GetTranslation<CardTranslation>
		GET "/card/translation" 
		authorize 
		queryParams 
			sourceValue 
			sourceLanguage 
			targetLanguage
		response
			targetValue
		

views 
	Card {
		insert(CardCreation)
		delete(CardDelete)
		update(CardUpdate)
		moveCards(MoveCards)
		bulkInsert(CsvUpload)
		changeCardOrder(ChangeCardOrderList)
	}

models 
	persistent Card {
		Unique PrimaryKey NotNull String cardId
		String given
		String wanted
		String image
		NotNull String cardAuthor
		NotNull Integer cardIndex
		NotNull String categoryId references com.anfelisa.category.Category.categoryId
		NotNull String rootCategoryId references com.anfelisa.category.Category.categoryId
	}

	CardUpdate {
		NotNull String cardId
		NotNull String given
		NotNull String wanted
		String image
		String userId
	}
	
	CardDelete {
		NotNull String cardId
		NotNull Integer cardIndex
		NotNull String categoryId
		String userId
	}
	CardSearch {
		String userId
		NotNull String given
		NotNull String wanted
		String categoryId
		List CardWithCategoryName cardList
		Boolean naturalInputOrder
	}
	
	CardWithCategoryName extends Card {
		String categoryName
	}
	
	CardTranslation {
		NotNull String sourceValue
		NotNull String targetValue
		NotNull String sourceLanguage
		NotNull String targetLanguage
	}
	
	CardCreation extends Card {
		String userId
		String username
	}
	
	CardList {
		String userId
		List CardWithInfo cardList
		String categoryId	
	}
	
	CardWithInfo extends Card {
		DateTime next
	}
	
	MoveCards {
		List String cardIdList
		String categoryId	
		String userId
		List Card movedCards
		List Card updatedIndices
	}

	ChangeCardOrderList {
		List String cardIdList
		String cardId	
		String userId
		List Card updatedIndices
	}

	CsvUpload {
		String userId
		NotNull String categoryId
		List SimpleCard previewCsv
		List Card cards
	}
	
	SimpleCard {
		NotNull String given
		NotNull String wanted
		NotNull String id
	}



