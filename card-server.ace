HttpServer

com.anfelisa.card

import com.anfelisa.user.AuthUser

ACE 
	CreateCard<CardCreation>
		POST "/card/create" 
		authorize 
		payload 
			wanted
			given
			image
			categoryId
		on ok (Card.insert)
	
	UpdateCard<CardUpdate>
		PUT "/card/update" 
		authorize 
		payload 
			cardId
			given
			image
			wanted
		on ok (Card.update)
	
	DeleteCard<CardDelete>
		DELETE "/card/delete"
		authorize 
		queryParams cardId
		on ok (Card.delete)
	
	GetCards<CardList>
		GET "/cards" 
		authorize 
		queryParams 
			categoryId 
		response
			cardList
			
	MoveCards<CardIdList>
		PUT "/cards/move"
		authorize
		payload
			cardIdList
			categoryId
		on ok (Card.moveCards)

	ImportCsv<CsvUpload>
		PUT "/category/import-csv"
		authorize
		payload
			previewCsv
			categoryId
		on ok (Card.bulkInsert)

views 
	Card {
		insert(CardCreation)
		delete(CardDelete)
		update(CardUpdate)
		moveCards(CardIdList)
		bulkInsert(CsvUpload)
	}

models 
	persistent Card {
		Unique PrimaryKey NotNull String cardId
		String given
		String wanted
		String image
		NotNull String cardAuthor
		NotNull Integer cardIndex
		NotNull String categoryId references com.anfelisa.category.Category.categoryId
		NotNull String rootCategoryId references com.anfelisa.category.Category.categoryId
	}

	CardUpdate {
		NotNull String cardId
		NotEmpty String given
		NotEmpty String wanted
		String image
		String userId
	}
	
	CardDelete {
		NotNull String cardId
		NotNull Integer cardIndex
		NotNull String categoryId
		String userId
	}
	CardSearch {
		NotNull String given
		NotNull String wanted
		String categoryId
		List CardWithCategoryName cardList
		Boolean naturalInputOrder
	}
	
	CardWithCategoryName extends Card {
		String categoryName
	}
	
	CardTranslation {
		NotEmpty String sourceValue
		NotEmpty String targetValue
		NotEmpty String sourceLanguage
		NotEmpty String targetLanguage
	}
	
	CardCreation extends Card {
		String userId
		String username
	}
	
	CardList {
		String userId
		List CardWithInfo cardList
		String categoryId	
	}
	
	CardWithInfo extends Card {
		DateTime next
	}
	
	CardIdList {
		List String cardIdList
		String categoryId	
		String userId
		List Card movedCards
	}

	CsvUpload {
		String userId
		NotNull String categoryId
		List SimpleCard previewCsv
		List Card cards
	}
	
	SimpleCard {
		NotNull String given
		NotNull String wanted
		NotNull String id
	}


