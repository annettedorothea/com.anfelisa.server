HttpServer Java Dropwizard JDBI3 Liquibase

com.anfelisa.category

import com.anfelisa.user.AuthUser

ACE	
	CreateCategory<CategoryCreation>
		POST "/category/create"  
		authorize
		payload 
			NotNull categoryName
			NotNull parentCategoryId
		on ok (Category.insert)
	
	UpdateCategory<CategoryUpdate>
		PUT "/category/update" 
		authorize
		payload 
			NotNull categoryId
			NotNull categoryName
		on ok (Category.update)
	
	DeleteCategory<CategoryDelete>
		DELETE "/category/delete"  
		authorize
		queryParams 
			NotNull categoryId
		on ok (Category.delete)
	
	GetCategoryTree<CategoryTree>
		GET "/category/tree" 
		authorize 
		queryParams 
			NotNull rootCategoryId
			NotNull filterNonScheduled
			priority
			NotNull reverse
		response
			rootCategory
			reverseBoxExists

	MoveCategory<CategoryMove>
		PUT "/category/move"
		authorize
		payload
			NotNull movedCategoryId
			NotNull targetCategoryId
		on ok (Category.moveCategory)
		on noMove
		
	ChangeOrderCategory<CategoryChangeOrder>
		PUT "/category/changeorder"
		authorize
		payload
			NotNull movedCategoryId
			NotNull targetCategoryId
		on ok (Category.changeOrder)
		on noMove
		
	InviteUserToCategory<UserToCategoryInvitation>
		PUT "/category/invite"
		authorize
		payload
			NotNull categoryId
			NotNull invitedUsername
		on ok (
			UserAccessToCategoryInvitation.invite
			BoxInvitation.createBox
		)
		
	GetInvitedUsers<AlreadyInvitedUsernames>
		GET "/category/invited-usernames"
		authorize
		queryParams
			NotNull categoryId
		response
			invitedUsernames

	SearchUsernameForInvitation <UsernameSearch>
		GET "/users/search"  
		authorize
		queryParams 
			NotNull usernameSearchString
			NotNull categoryId
		response
			usernames
	
		
views 
	Category {
		insert(CategoryCreation)
		delete(CategoryDelete)
		update(CategoryUpdate)
		moveCategory(CategoryMove)
		changeOrder(CategoryChangeOrder)
	}
	
	UserAccessToCategoryInvitation {
		invite(UserToCategoryInvitation)
	}
	BoxInvitation {
		createBox (UserToCategoryInvitation)
	}
	
models 
	persistent Category {
		Unique PrimaryKey NotNull String categoryId
		NotNull String categoryName
		NotNull String categoryAuthor
		NotNull Integer categoryIndex
		String parentCategoryId references categoryId
		String rootCategoryId references categoryId
		Boolean dictionaryLookup
		String givenLanguage
		String wantedLanguage
	}
	
	persistent UserAccessToCategory {
		PrimaryKey NotNull String categoryId references Category.categoryId
		PrimaryKey NotNull String userId references com.anfelisa.user.User.userId
		Boolean editable
	}
	
	CategoryUpdate {
		String categoryId
		String categoryName
		String userId
	}
	
	CategoryDelete {
		String categoryId
		Integer categoryIndex
		String parentCategoryId
		String userId
	}
	
	CategoryTree {
		CategoryTreeItem rootCategory
		String userId
		String rootCategoryId
		Boolean filterNonScheduled
		Integer priority
		Boolean editable
		Boolean reverse
		Boolean reverseBoxExists
	}

	CategoryTreeItem {
		String categoryId
		String categoryName
		Integer categoryIndex
		Boolean empty
		String parentCategoryId
		Boolean dictionaryLookup
		String givenLanguage
		String wantedLanguage
		String rootCategoryId
		List CategoryTreeItem childCategories
		Integer nonScheduledCount
		Boolean editable
	}

	UserWithAccessList {
		String categoryId
		List UserWithAccess userList
	}
	
	UserWithAccess {
		String userId
		String username
		Boolean editable
	}
	
	CategoryCreation extends Category {
		String username
		String userId
	}
	
	CategoryMove {
		String movedCategoryId
		String targetCategoryId
		Category movedCategory
		Integer categoryIndexWhereRemoved
		String parentCategoryIdWhereRemoved
		String userId
	}
	
	CategoryChangeOrder {
		String movedCategoryId
		String targetCategoryId
		String userId
		List Category updatedIndices
	}

	UserToCategoryInvitation {
		String categoryId
		String userId
		String invitedUsername
		String invitedUserId
		com.anfelisa.box.Box boxForInvitedUser
	}

	AlreadyInvitedUsernames {
		String categoryId
		String userId
		List String invitedUsernames
	}

	UsernameSearch {
		String usernameSearchString
		String userId
		String categoryId
		List String usernames
	}


