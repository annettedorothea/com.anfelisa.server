HttpServer Java 

com.anfelisa.category.inviteusertocategory

scenarios 

	InviteUserToCategory
		GIVEN
			com.anfelisa.box.createbox.CreateBoxMinimal
			com.anfelisa.user.registeruser.RegisterTwoUsers excludeGIVEN
		WHEN
			com.anfelisa.category.InviteUserToCategory 
			uuid "boxIdOfInvitedUser-${testId}"
			{
				categoryId: "boxId-${testId}",
				invitedUsername: "Anne-${testId}"
			}
			authorization "Annette-${testId}" : "password"
		THEN
			200
			persistence accessToCategoryWasGranted UserAccessToCategory selectByPrimaryKey ( categoryId: "boxId-${testId}", userId: "uuid2-${testId}") shouldBe
			{
				categoryId: "boxId-${testId}",
				editable: false,
				userId: "uuid2-${testId}"
			}
			boxWasCreated Box selectByPrimaryKey ( boxId: "boxIdOfInvitedUser-${testId}" ) shouldBe
			{
				categoryId: "boxId-${testId}",
				userId: "uuid2-${testId}",
				boxId: "boxIdOfInvitedUser-${testId}",
				maxCardsPerDay: 10,
				maxInterval: null
			}
			
	InviteUserToCategoryThatDoesNotExist
		GIVEN
			com.anfelisa.box.createbox.CreateBoxMinimal
			com.anfelisa.user.registeruser.RegisterTwoUsers excludeGIVEN
		WHEN
			com.anfelisa.category.InviteUserToCategory {
				categoryId: "boxId123-${testId}",
				invitedUsername: "Anne-${testId}"
			}
			authorization "Annette-${testId}" : "password"
		THEN
			400
			
	InviteUserToCategorySelf
		GIVEN
			com.anfelisa.box.createbox.CreateBoxMinimal
		WHEN
			com.anfelisa.category.InviteUserToCategory {
				categoryId: "boxId-${testId}",
				invitedUsername: "Annette-${testId}"
			}
			authorization "Annette-${testId}" : "password"
		THEN
			400
			persistence accessToCategoryWasNotChanged UserAccessToCategory selectByPrimaryKey ( categoryId: "boxId-${testId}", userId: "uuid-${testId}") shouldBe
			{
				categoryId: "boxId-${testId}",
				editable: true,
				userId: "uuid-${testId}"
			}
			
	InviteUserSecondTime
		GIVEN
			InviteUserToCategory
		WHEN
			com.anfelisa.category.InviteUserToCategory {
				categoryId: "boxId-${testId}",
				invitedUsername: "Anne-${testId}"
			}
			authorization "Annette-${testId}" : "password"
		THEN
			400
			persistence accessToCategoryWasNotChanged UserAccessToCategory selectByPrimaryKey ( categoryId: "boxId-${testId}", userId: "uuid2-${testId}") shouldBe
			{
				categoryId: "boxId-${testId}",
				editable: false,
				userId: "uuid2-${testId}"
			}
			
	InviteUserToCategoryInvitedUserDoesNotExist
		GIVEN
			com.anfelisa.box.createbox.CreateBoxMinimal
		WHEN
			com.anfelisa.category.InviteUserToCategory {
				categoryId: "boxId-${testId}",
				invitedUsername: "X-${testId}"
			}
			authorization "Annette-${testId}" : "password"
		THEN
			400
			
	InviteUserToCategoryNoAccessToCategory
		GIVEN
			com.anfelisa.box.createbox.CreateBoxMinimal
			com.anfelisa.user.registeruser.RegisterTwoUsers excludeGIVEN
		WHEN
			com.anfelisa.category.InviteUserToCategory {
				categoryId: "boxId-${testId}",
				invitedUsername: "Annette-${testId}"
			}
			authorization "Anne-${testId}" : "pw"
		THEN
			401
			
	InviteUserToCategoryNoEditingAccessToCategory
		GIVEN
			InviteUserToCategory
			com.anfelisa.user.registeruser.RegisterTwoUsers excludeGIVEN
			com.anfelisa.user.registeruser.RegisterUserAdmin
		WHEN
			com.anfelisa.category.InviteUserToCategory {
				categoryId: "boxId-${testId}",
				invitedUsername: "Admin"
			}
			authorization "Anne-${testId}" : "pw"
		THEN
			401
			
	InviteUserToCategoryUnauthorized
		GIVEN
			com.anfelisa.box.createbox.CreateBoxMinimal
		WHEN
			com.anfelisa.category.InviteUserToCategory {
				categoryId: "boxId-${testId}",
				invitedUsername: "Anne-${testId}"
			}
		THEN
			401
